// Copyright 2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/os"
use "std/os/filepath"
use "std/jule"
use "std/jule/parser"
use "std/jule/sema"
use "std/jule/token"
use "std/strings"

fn formatFile(path: str) {
	mut file := token::Fileset.New(path)
	unsafe {
		file.FillMut(os::File.Read(path) else {
			println("error: file could not read: " + path)
			ret
		})
	}
	mut errors := token::Lex(file, token::LexMode.Comment)
	if len(errors) > 0 {
		println("error: file could not formatted, have error(s): " + path)
		ret
	}

	mut cm := commentMap.build(file.Tokens)
	mut finfo := parser::ParseFile(file)
	if len(finfo.Errors) > 0 {
		println("error: file could not formatted, have error(s): " + path)
		ret
	}

	mut f := finfo.Ast
	fmt := formatter.new()
	buf := fmt.format(f, cm)
	fpath := f.File.Path
	os::File.Write(fpath, buf, 0o660) else {
		println("error: could not write to file: " + fpath)
		ret
	}
}

fn parsePackage(path: str) {
	mut dirents := os::Dir.Read(path) else {
		println("error: connot read package directory: " + path)
		ret
	}
	for _, dirent in dirents {
		// Skip directories, and non-jule files.
		if dirent.Stat.IsDir() || !strings::HasSuffix(dirent.Name, jule::Ext) {
			continue
		}
		filepath := filepath::Join(path, dirent.Name)
		formatFile(filepath)
	}
}

fn main() {
	args := os::Args()
	if len(args) == 1 {
		ret
	}
	parsePackage(args[1])
}