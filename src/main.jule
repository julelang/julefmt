// Copyright 2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use env for std::env
use std::fs::{Directory, File}
use path for std::fs::path
use process for std::process
use build for std::jule::build
use lex for std::jule::lex
use parser for std::jule::parser
use sema for std::jule::sema::{
    SemaFlag,
}
use strings for std::strings

fn formatFile(path: str) {
    let mut file = lex::NewFileSet(path)
    file.Fill(File.Read(path) else {
        outln("error: file could not read: " + path)
        ret
    })
    let mut errors = lex::Lex(file, lex::LexMode.Comment)
    if errors.len > 0 {
        outln("error: file could not formatted, have error(s): " + path)
        ret
    }

    let mut cm = commentMap.build(file.Tokens)
    let mut finfo = parser::ParseFile(file)
    if finfo.Errors.len > 0 {
        outln("error: file could not formatted, have error(s): " + path)
        ret
    }

    let mut f = finfo.Ast
    let fmt = formatter.new()
    let buf = fmt.format(f, cm)
    let fpath = f.File.Path
    File.Write(fpath, []byte(buf), 0o660) else {
        outln("error: could not write to file: " + fpath)
        ret
    }
}

fn parsePackage(path: str) {
    let mut dirents = Directory.Read(path) else {
        outln("error: connot read package directory: " + path)
        ret
    }
    for _, dirent in dirents {
        // Skip directories, and non-jule files.
        if dirent.Stat.IsDir() || !strings::HasSuffix(dirent.Name, build::Ext) {
            continue
        }
        let filepath = path::Join(path, dirent.Name)
        formatFile(filepath)
    }
}

fn main() {
    let args = env::Args()
    if args.len == 1 {
        ret
    }
    parsePackage(args[1])
}